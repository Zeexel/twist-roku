<?xml version = "1.0" encoding = "utf-8" ?>
<component name = "twistApiCall" extends = "Task" >

  <interface>
    <field id = "url" type = "string" />
    <field id = "token" type = "string" />
    <field id = "title_field" type = "string" />
    <field id = "title_prefix" type = "string" />
    <field id = "content" type = "node" />
  </interface>

  <script type = "text/brightscript" >

    <![CDATA[
      sub init()
        m.top.functionName = "getContent"
      end sub

      sub getContent()
        print "Querying AnimeTwist API for anime list!"
        content = createObject("roSGNode", "ContentNode")

        url = createObject("roUrlTransfer")
        url.SetCertificatesFile("common:/certs/ca-bundle.crt")
        url.AddHeader("X-Roku-Reserved-Dev-Id", "")
        url.InitClientCertificates()

        url.setUrl(m.top.url)
        url.AddHeader("x-access-token", m.top.token)
        
        response = parseJSON(url.GetToString())

        for each item in response
          listitem = content.createChild("ContentNode")
          listitem.addFields({ "twist_data" : item })

          itemTitle = item[m.top.title_field]
          if type(itemTitle) = "String"
            listitem.title = m.top.title_prefix + itemTitle
          else if type(itemTitle) = "Integer"
            listitem.title = m.top.title_prefix + StrI(itemTitle)
          end if
        end for

        m.top.content = content

        print "Done fetching anime list!"
      end sub
    ]]>

  </script>

</component>